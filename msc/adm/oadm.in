#!/usr/bin/perl
use warnings; use strict;

my @cmds = qw/create dash delete dxstart osh password projlist webstart/;
my %cmds = (); @cmds{@cmds} = ();

my @needarg = qw/create dash delete osh password/;
my %needarg = (); @needarg{@needarg} = ();

my($cmd,@args) = @ARGV;

if ($cmd) {
    if (exists $cmds{$cmd}) {
	if (exists $needarg{$cmd}) {
	    die "oadm: please specify a project for the $cmd command\n"
		unless $args[0];
	}
	{ no strict;
	  &{$cmd}($args[0]);
	}
    } else {
	die "oadm: unknown command $cmd. Stop";
    }
} else {
    usage();
}

##############################################################

sub user_exec {
    warn "exec @_\n";
    exec @_;
    exit 0;
}

sub sudo_exec {
    warn "oadmsudo @_\n";
    exec "$ENV{'ORACC'}/sbin/oadmsudo", @_;
    exit 0;
}

sub dash {
    shell("/usr/bin/dash", $_[0]);
}

sub dxstart {
    sudo_exec "$ENV{'ORACC'}/sbin/dx-start.sh";
}

sub create {
    sudo_exec "$ENV{'ORACC'}/sbin/newproj.sh", $_[0];
}

sub delete {
    sudo_exec "$ENV{'ORACC'}/sbin/delproj.sh", $_[0];
}

sub osh {
    shell("$ENV{'ORACC'}/bin/osh", $_[0]);
}

sub password {
    sudo_exec "/usr/bin/passwd", $_[0];
}

sub projlist {
    sudo_exec "$ENV{'ORACC'}/bin/agg-project-list.sh";
}

sub shell {
    my($sh,$project) = @_;
    sudo_exec "/usr/bin/chsh", "-s", $sh, $project;
}

sub webstart {
    sudo_exec "$ENV{'ORACC'}/sbin/webstart.sh";
}

sub usage {
print <<EOD;

oadm -- the Oracc2 Administrator\'s Management tool.

Usage:

    oadm COMMAND [ARG]

Commands are:

    create [PROJECT]   -- create project

    dash [PROJECT]     -- set project shell to /bin/dash

    delete [PROJECT]   -- delete project

    dxstart            -- restart oracc command server dx

    osh [PROJECT]      -- set project shell to the Oracc shell, osh

    password [PROJECT] -- reset password for project

    projlist           -- rebuild project list

    webstart           -- restart webserver

EOD
exit 0;
}

1;
